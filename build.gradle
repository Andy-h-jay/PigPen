// Establish version and status
ext.githubProjectName = rootProject.name

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://clojars.org/repo' }
    }
    dependencies {
        classpath 'clojuresque:clojuresque:1.7.0'
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'http://clojars.org/repo' }
        maven { url 'http://conjars.org/repo' }
    }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {

    group = 'com.netflix.pigpen'

    license {
        mapping('clj', 'SEMICOLON_STYLE')
    }

    jar {
       from 'src/main/clojure'
    }

    apply plugin: 'clojure'
    tasks.uberjar.enabled=true
    clojure.aotCompile = true

    compileClojure {
        jvmOptions {
            jvmArgs '-Djava.awt.headless=true'
        }
        classpath = project.files(
            project.compileJava.outputs,
            classpath
        )
    }

    clojureTest {
        // this is here because pig and hadoop are provided dependencies
        // without this, clojureTest doesn't work
        classpath = sourceSets.test.runtimeClasspath
    }

    clojuredoc {
        includeNamespace 'pigpen.core'
        includeNamespace 'pigpen.fold'
        codox = [
            srcDirUri: 'https://github.com/Netflix/PigPen/blob/master/',
            srcLinenumAnchorPrefix: 'L'
        ]
    }

    javadoc {
        // this is here because pig and hadoop are provided dependencies
        // without this, javadoc doesn't work
        classpath = sourceSets.test.runtimeClasspath
    }

    apply plugin: 'eclipse'
    eclipse {
        // this is here because pig and hadoop are provided dependencies
        // without this, counterclockwise doesn't work
        classpath {
            plusConfigurations += configurations.provided
        }
    }

    ////////////////////////////////////////////////////////////////////////////////
    // Define a task that runs an nrepl server. The port is given with the nreplPort
    // property:
    //    gradlew nrepl -PnreplPort=9999
    // or put the property in ~/.gradle/gradle.properties
    configurations { nrepl }
    dependencies {
      nrepl 'org.clojure:tools.nrepl:0.2.7'
      nrepl 'org.clojure:clojure:1.6.0'
    }
    task nrepl(type: JavaExec) {
        classpath project.sourceSets.main.clojure.srcDirs,
                  project.sourceSets.test.clojure.srcDirs,
                  sourceSets.test.runtimeClasspath,
                  sourceSets.main.runtimeClasspath,
                  configurations.nrepl

        main = "clojure.main"
        args '--eval', "(ns gradle-nrepl (:require [clojure.tools.nrepl.server :refer (start-server stop-server)]))",
             '--eval', "(println \"Starting nrepl server on port $nreplPort\")",
             '--eval', "(def server (start-server :port $nreplPort))"
    }
}
